plugins {
  id 'java'
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'org.beryx.jlink' version '3.0.1'
}

group = 'com.example'
version = '1.1.0'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.openjfx:javafx-controls:21'
  implementation 'org.openjfx:javafx-fxml:21'
  implementation 'org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.2'
  implementation 'org.slf4j:slf4j-api:2.0.13'
  implementation 'ch.qos.logback:logback-classic:1.5.18'
  implementation 'org.apache.poi:poi-ooxml:5.4.1'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

application {
  mainModule = 'com.example.mentis'
  mainClass = 'com.example.mentis.application.MentisApplication'
}

javafx {
  version = '21-ea+5'
  modules = ['javafx.controls', 'javafx.fxml']
}

jar {
  manifest {
    attributes(
            'Main-Class': 'com.example.mentis.application.MentisApplication'
    )
  }
}

shadowJar {
  archiveClassifier.set("all")
  mergeServiceFiles()
}

def x64JdkHome = "/Library/Java/JavaVirtualMachines/temurin-21.jdk/Contents/Home"


jlink {
  imageName = 'MentisApp'
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

  launcher {
    name = 'mentis'
  }

  jpackage {
    imageName = 'Mentis'
    installerName = 'MentisInstaller'
    // imageName = 'Mentis_x64'
    // installerName = 'MentisInstaller_x64'
    skipInstaller = false
    installerType = System.getProperty('os.name').toLowerCase().contains('mac') ? 'dmg' :
            System.getProperty('os.name').toLowerCase().contains('win') ? 'exe' : 'deb'
    appVersion = '1.1.0'
    icon = 'src/main/resources/com/example/mentis/assets/AppIcon.icns'
    // jpackageHome = x64JdkHome
  }

}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

test {
  useJUnitPlatform()
}
